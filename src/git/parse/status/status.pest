branch        = @{ !(".." | NEWLINE | " ") ~ ANY ~ branch | "" }
local         = @{ branch }
remote        = @{ branch }
ahead         =  { ASCII_DIGIT+ }
behind        =  { ASCII_DIGIT+ }
ahead_behind  = _{ "[" ~ (("ahead " ~ ahead | "behind " ~ behind) ~ ", "?)+ ~ "]" }
no_commits    =  { "No commits yet on " ~ local }
no_branch     =  { "HEAD (no branch)" }
branch_status =  { "## " ~ (no_commits | no_branch | local ~ ("..." ~ remote)? ~ (" " ~ ahead_behind | " [gone]")?) }
file          =  { (!(NEWLINE | " -> ") ~ ANY)+ }
new_file      = @{ file }
code          =  { (ASCII_ALPHA | "?" | " "){2} }
file_status   =  { code ~ " " ~ file ~ (" -> " ~ new_file)? }
no_repo       = { "" }
status_lines  = _{ branch_status ~ NEWLINE ~ (file_status ~ NEWLINE)* | no_repo }
